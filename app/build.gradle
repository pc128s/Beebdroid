import org.apache.tools.ant.taskdefs.condition.Os

apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion '28.0.2'
    defaultConfig {
        applicationId "com.littlefluffytoys.beebdroid"
        targetSdkVersion 23
        minSdkVersion 13
    }
    useLibrary 'org.apache.http.legacy'
    sourceSets.main {
        jniLibs.srcDir 'src/main/libs' //set .so files location to libs instead of jniLibs
        jni.srcDirs = [] //disable automatic ndk-build call
    }
    // add a task that calls regular ndk-build(.cmd) script from app directory
    task ndkBuild(type: Exec) {
        if (Os.isFamily(Os.FAMILY_WINDOWS)) {
            commandLine 'ndk-build.cmd', '-C', file('src/main').absolutePath
        } else {
            Properties properties = new Properties()
            properties.load(project.rootProject.file('local.properties').newDataInputStream())
            def ndkDir = properties.getProperty('ndk.dir')
            if (ndkDir == null) {
                def sdkDir = properties.getProperty('sdk.dir')
                ndkDir = "${sdkDir}/ndk"
                def candidates = new File(ndkDir).grep({ new File(it, "ndk-build").exists() })
                if (candidates.size() != 1) {
                    throw new RuntimeException("Wanted one NDK in local.properties:sdk.dir but found ${candidates.size()}")
                }
                ndkDir = candidates[0]
            }
            commandLine "${ndkDir}/ndk-build", '-C', file('src/main').absolutePath
        }
    }
    // add this task as a dependency of Java compilation
    tasks.withType(JavaCompile) {
        compileTask -> compileTask.dependsOn ndkBuild
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
}
android {
    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:23.0.1'
    compile 'com.android.support:recyclerview-v7:23.0.1'
    compile 'com.android.support:support-annotations:23.0.1'
    compile 'com.android.support:support-v13:23.0.1'
    compile 'com.android.support:support-v4:23.0.1'
}

repositories {
    maven {
        url "http://maven.ouya.tv/"
    }
}

